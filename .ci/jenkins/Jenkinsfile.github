#!/usr/bin/groovy

/**
 * Copyright (c) NVIDIA CORPORATION & AFFILIATES, 2026. ALL RIGHTS RESERVED.
 * See file LICENSE for terms.
 */

@Library('blossom-github-lib@master')
@Library('github.com/Mellanox/ci-demo@master')
def matrix = new com.mellanox.cicd.Matrix()

import ipp.blossom.*

def githubHelper = null
def blueOceanUrl = ""
def currentPrNumber = null

if (params.githubData) {
  def slurper = new groovy.json.JsonSlurper()
  currentPrNumber = slurper.parseText(params.githubData)?.pr?.toString()

  // Abort previous running builds of the same PR
  if (currentPrNumber) {
    try {
      Jenkins.instance.getItemByFullName(env.JOB_NAME).builds.findAll {
        it.isBuilding() && it.number < currentBuild.number
      }.each { b ->
        def gd = b.getAction(hudson.model.ParametersAction)?.getParameters()?.find { it.name == 'githubData' }?.value
        if (gd && slurper.parseText(gd)?.pr?.toString() == currentPrNumber) {
          echo "Aborting build #${b.number} for same PR #${currentPrNumber}"
          b.doStop()
        }
      }
    } catch (Exception e) {
      echo "Could not abort previous builds: ${e.message}"
    }
  }
  withCredentials([usernamePassword(credentialsId: 'github-token', passwordVariable: 'GIT_PASSWORD', usernameVariable: 'GIT_USERNAME')]) {
    githubHelper = GithubHelper.getInstance("${GIT_PASSWORD}", params.githubData)
  }

  def blueOceanJobPath = env.JOB_NAME.replace('/', '%2F')
  blueOceanUrl = "${JENKINS_URL}blue/organizations/jenkins/${blueOceanJobPath}/detail/${env.JOB_BASE_NAME}/${env.BUILD_NUMBER}/pipeline/"

  githubHelper.updateCommitStatus(blueOceanUrl, "CI started", GitHubCommitState.PENDING, env.JOB_BASE_NAME)
  currentBuild.description = githubHelper.getBuildDescription()
}

try {
  matrix.main()
} catch (Exception ex) {
  println ex
  throw ex
} finally {
  if (params.githubData) {
    githubHelper.updateCommitStatus(
      blueOceanUrl,
      "CI completed",
      currentBuild.resultIsBetterOrEqualTo('SUCCESS') ? GitHubCommitState.SUCCESS : GitHubCommitState.FAILURE,
      env.JOB_BASE_NAME
    )
    githubHelper.uploadLogs(this, env.JOB_NAME, env.BUILD_NUMBER, null, null)
  }
}
